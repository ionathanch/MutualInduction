import CBPV.Evaluation
import CBPV.Typing

open ValType ComType Val Com

/-*--------------------------
  Logical relation on types
--------------------------*-/

mutual
@[simp]
def ğ’± (A : ValType) (v : Val) : Prop :=
  match A with
  | .Unit => v = unit
  | .Sum Aâ‚ Aâ‚‚ => (âˆƒ w, ğ’± Aâ‚ w âˆ§ v = inl w) âˆ¨ (âˆƒ w, ğ’± Aâ‚‚ w âˆ§ v = inr w)
  | U B => âˆƒ m, â„° B m âˆ§ v = thunk m

@[simp]
def ğ’ (B : ComType) (m : Com) : Prop :=
  match B with
  | F A => âˆƒ v, ğ’± A v âˆ§ m = ret v
  | Arr A B => âˆƒ n, (âˆ€ v, ğ’± A v â†’ â„° B (nâ¦ƒvâ¦„)) âˆ§ m = lam n
  | .Prod Bâ‚ Bâ‚‚ => âˆƒ nâ‚ nâ‚‚, â„° Bâ‚ nâ‚ âˆ§ â„° Bâ‚‚ nâ‚‚ âˆ§ m = prod nâ‚ nâ‚‚

@[simp]
def â„° (B : ComType) (m : Com) := âˆƒ n, m â‡“ n âˆ§ ğ’ B n
end
notation:40 v:41 "âˆˆ" "âŸ¦" A:41 "âŸ§áµ›" => ğ’± A v
notation:40 m:41 "âˆˆ" "âŸ¦" B:41 "âŸ§á¶œ" => ğ’ B m
notation:40 m:41 "âˆˆ" "âŸ¦" B:41 "âŸ§áµ‰" => â„° B m

-- Convenient constructors for the logical relation
theorem ğ’±.unit : ğ’± Unit unit := by simp
theorem ğ’±.inl {v Aâ‚ Aâ‚‚} (h : ğ’± Aâ‚ v) : ğ’± (Sum Aâ‚ Aâ‚‚) (inl v) := by simp; assumption
theorem ğ’±.inr {v Aâ‚ Aâ‚‚} (h : ğ’± Aâ‚‚ v) : ğ’± (Sum Aâ‚ Aâ‚‚) (inr v) := by simp; assumption
theorem ğ’±.thunk {m B} (h : â„° B m) : ğ’± (U B) (thunk m) := by simp at *; assumption
theorem ğ’.ret {v A} (h : ğ’± A v) : ğ’ (F A) (ret v) := by simp; assumption
theorem ğ’.lam {n A B} (h : âˆ€ v, ğ’± A v â†’ â„° B (nâ¦ƒvâ¦„)) : ğ’ (Arr A B) (lam n) := by simp at *; assumption
theorem ğ’.prod {m n Bâ‚ Bâ‚‚} (hm : â„° Bâ‚ m) (hn : â„° Bâ‚‚ n) : ğ’ (Prod Bâ‚ Bâ‚‚) (prod m n) := by simp at *; constructor <;> assumption

-- Semantic computations are normal
theorem ğ’nf {B m} (h : m âˆˆ âŸ¦ B âŸ§á¶œ) : nf m :=
  match (generalizing := true) B with
  | F _ | Arr _ _ =>
    by simp at h; let âŸ¨_, _, eâŸ© := h; subst e; exact âŸ¨âŸ©
  | .Prod _ _ =>
    by simp at h; let âŸ¨_, _, _, _, eâŸ© := h; subst e; exact âŸ¨âŸ©

-- Semantic computations embed into semantic evaluations
theorem ğ’â„° {B m} (h : m âˆˆ âŸ¦ B âŸ§á¶œ) : m âˆˆ âŸ¦ B âŸ§áµ‰ :=
  by simp; exact âŸ¨m, âŸ¨.refl, ğ’nf hâŸ©, hâŸ©

-- Semantic evaluations are backward closed under reduction
theorem â„°bwd {B m n} (r : m â‡’â‹† n) (h : n âˆˆ âŸ¦ B âŸ§áµ‰) : m âˆˆ âŸ¦ B âŸ§áµ‰ := by
  unfold â„° at *
  let âŸ¨n', âŸ¨r', nfnâŸ©, hâŸ© := h
  refine âŸ¨n', âŸ¨.trans' r r', nfnâŸ©, hâŸ©
theorem ğ’bwd {B m n} (r : m â‡’â‹† n) (h : n âˆˆ âŸ¦ B âŸ§á¶œ) : m âˆˆ âŸ¦ B âŸ§áµ‰ := â„°bwd r (ğ’â„° h)

/-*----------------
  Semantic typing
----------------*-/

-- Semantic well-formedness of contexts
def semCtxt Î“ (Ïƒ : Nat â†’ Val) := âˆ€ {x A}, Î“ âˆ‹ x âˆ¶ A â†’ Ïƒ x âˆˆ âŸ¦ A âŸ§áµ›
notation:40 Î“:41 "âŠ¨" Ïƒ:41 => semCtxt Î“ Ïƒ

-- Convenient constructors for semantic contexts
theorem semCtxtNil : â¬ âŠ¨ var := by intro _ _ mem; cases mem
theorem semCtxtCons {Î“ Ïƒ v A} (h : v âˆˆ âŸ¦ A âŸ§áµ›) (hÏƒ : Î“ âŠ¨ Ïƒ) : Î“ âˆ· A âŠ¨ v +: Ïƒ
  | _, _, .here => h
  | _, _, .there mem => hÏƒ mem

-- Semantic typing of values and computations
@[reducible, simp] def semVal Î“ v A := âˆ€ Ïƒ, Î“ âŠ¨ Ïƒ â†’ vâ¦ƒÏƒâ¦„ âˆˆ âŸ¦ A âŸ§áµ›
@[reducible, simp] def semCom Î“ m B := âˆ€ Ïƒ, Î“ âŠ¨ Ïƒ â†’ mâ¦ƒÏƒâ¦„ âˆˆ âŸ¦ B âŸ§áµ‰
notation:40 Î“:41 "âŠ¨" v:41 "âˆ¶" A:41 => semVal Î“ v A
notation:40 Î“:41 "âŠ¨" m:41 "âˆ¶" B:41 => semCom Î“ m B

/-*----------------------------------------
  Fundamental theorem of soundness
  of syntactic typing wrt semantic typing
----------------------------------------*-/

theorem soundness {Î“} :
  (âˆ€ (v : Val) A, Î“ âŠ¢ v âˆ¶ A â†’ Î“ âŠ¨ v âˆ¶ A) âˆ§
  (âˆ€ (m : Com) B, Î“ âŠ¢ m âˆ¶ B â†’ Î“ âŠ¨ m âˆ¶ B) := by
  refine âŸ¨Î» v A h â†¦ ?val, Î» m B h â†¦ ?comâŸ©
  mutual_induction h, h
  all_goals intro Ïƒ hÏƒ
  case var mem => exact hÏƒ mem
  case unit => exact ğ’±.unit
  case inl ih => exact ğ’±.inl (ih Ïƒ hÏƒ)
  case inr ih => exact ğ’±.inr (ih Ïƒ hÏƒ)
  case thunk ih => exact ğ’±.thunk (ih Ïƒ hÏƒ)
  case force ih =>
    simp at ih
    let âŸ¨_, âŸ¨_, âŸ¨r, _âŸ©, hâŸ©, eâŸ© := ih Ïƒ hÏƒ
    let rf : _ â‡’â‹† _ := .trans .force r
    rw [â† e] at rf
    exact ğ’bwd rf h
  case lam ih =>
    apply ğ’â„°; apply ğ’.lam
    intro v hv; rw [â† substUnion]
    exact ih (v +: Ïƒ) (semCtxtCons hv hÏƒ)
  case app ihm ihv =>
    simp at ihm
    let âŸ¨_, âŸ¨rlam, _âŸ©, _, h, eâŸ© := ihm Ïƒ hÏƒ; subst e
    let âŸ¨_, âŸ¨rval, _âŸ©, hâŸ© := h _ (ihv Ïƒ hÏƒ)
    exact ğ’bwd (Trans.trans (Evals.app rlam) (Trans.trans Eval.lam rval)) h
  case ret ih => exact ğ’â„° (ğ’.ret (ih Ïƒ hÏƒ))
  case letin ihret ih =>
    simp at ihret ih
    let âŸ¨_, âŸ¨rret, _âŸ©, v, hv, eâŸ© := ihret Ïƒ hÏƒ; subst e
    let âŸ¨_, âŸ¨rlet, nfletâŸ©, hâŸ© := ih (v +: Ïƒ) (semCtxtCons hv hÏƒ)
    rw [substUnion] at rlet
    exact ğ’bwd (Trans.trans (Evals.let rret) (Trans.trans Eval.ret rlet)) h
  case case m n _ _ _ _ _ _ ihv ihm ihn =>
    simp at ihv
    match ihv Ïƒ hÏƒ with
    | .inl âŸ¨v, hv, eâŸ© =>
      let hm := ihm (v +: Ïƒ) (semCtxtCons hv hÏƒ)
      simp only [substCom]; rw [e]; rw [substUnion] at hm
      exact â„°bwd (.once .inl) hm
    | .inr âŸ¨v, hv, eâŸ© =>
      let hn := ihn (v +: Ïƒ) (semCtxtCons hv hÏƒ)
      simp only [substCom]; rw [e]; rw [substUnion] at hn
      exact â„°bwd (.once .inr) hn
  case prod m n _ _ _ _ ihm ihn =>
    simp at ihm ihn
    let âŸ¨_, âŸ¨rm, _âŸ©, hmâŸ© := ihm Ïƒ hÏƒ
    let âŸ¨_, âŸ¨rn, _âŸ©, hnâŸ© := ihn Ïƒ hÏƒ
    apply ğ’â„°; exact ğ’.prod (ğ’bwd rm hm) (ğ’bwd rn hn)
  case prjl ih =>
    simp [-ğ’] at ih; unfold ğ’ at ih
    let âŸ¨_, âŸ¨rprod, nfprodâŸ©, nâ‚, nâ‚‚, hm, _, eâŸ© := ih Ïƒ hÏƒ; subst e
    let r : prjl (_â¦ƒÏƒâ¦„) â‡’â‹† nâ‚ := Trans.trans (Evals.prjl rprod) Eval.prodl
    exact â„°bwd r hm
  case prjr ih =>
    simp [-ğ’] at ih; unfold ğ’ at ih
    let âŸ¨_, âŸ¨rprod, nfprodâŸ©, nâ‚, nâ‚‚, _, hn, eâŸ© := ih Ïƒ hÏƒ; subst e
    let r : prjr (_â¦ƒÏƒâ¦„) â‡’â‹† nâ‚‚ := Trans.trans (Evals.prjr rprod) Eval.prodr
    exact â„°bwd r hn

-- If a computation does not step, then it is in normal form
theorem normal {m B} (nr : âˆ€ {n}, Â¬ m â‡’ n) (h : â¬ âŠ¢ m âˆ¶ B) : nf m := by
  let âŸ¨_, soundComâŸ© := soundness (Î“ := â¬)
  let mB := soundCom m B h
  simp at mB
  let âŸ¨_, âŸ¨r, nfmâŸ©, _âŸ© := mB var semCtxtNil
  rw [substComId] at r
  cases r with | refl => exact nfm | trans r _ => cases nr r

-- Computations are strongly normalizing
theorem normalization {m : Com} {B : ComType} (h : â¬ âŠ¢ m âˆ¶ B) : SN m := by
  let âŸ¨_, soundComâŸ© := soundness (Î“ := â¬)
  let mB := soundCom m B h
  simp at mB
  let âŸ¨_, âŸ¨r, nfmâŸ©, _âŸ© := mB var semCtxtNil
  rw [substComId] at r
  exact r.sn nfm
