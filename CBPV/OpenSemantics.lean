import CBPV.NormalInd

open ValType ComType Val Com

/-*--------------------------
  Logical relation on types
--------------------------*-/

section
set_option hygiene false
local notation:40 "âŸ¦" A:41 "âŸ§áµ›" "â†˜" P:41 => ğ’± A P
local notation:40 "âŸ¦" B:41 "âŸ§á¶œ" "â†˜" P:41 => ğ’ B P

mutual
inductive ğ’± : ValType â†’ (Val â†’ Prop) â†’ Prop where
  | Unit : âŸ¦ Unit âŸ§áµ› â†˜ (Î» v â†¦ SNeVal v âˆ¨ v = unit)
  | Sum {Aâ‚ Aâ‚‚ P Q} :
    âŸ¦ Aâ‚ âŸ§áµ› â†˜ P â†’
    âŸ¦ Aâ‚‚ âŸ§áµ› â†˜ Q â†’
    ----------------------------------
    âŸ¦ Sum Aâ‚ Aâ‚‚ âŸ§áµ› â†˜ (Î» v â†¦ SNeVal v âˆ¨
      (âˆƒ w, v = inl w âˆ§ P w) âˆ¨
      (âˆƒ w, v = inr w âˆ§ Q w))
  | U {B P} :
    âŸ¦ B âŸ§á¶œ â†˜ P â†’
    ------------------------------
    âŸ¦ U B âŸ§áµ› â†˜ (Î» v â†¦ P (force v))

inductive ğ’ : ComType â†’ (Com â†’ Prop) â†’ Prop where
  | F {A P} :
    âŸ¦ A âŸ§áµ› â†˜ P â†’
    ----------------------------------------------------------------------
    âŸ¦ F A âŸ§á¶œ â†˜ (Î» m â†¦ (âˆƒ n, m â¤³â‹† n âˆ§ SNeCom n) âˆ¨ (âˆƒ v, m â¤³â‹† ret v âˆ§ P v))
  | Arr {A B P Q} :
    âŸ¦ A âŸ§áµ› â†˜ P â†’
    âŸ¦ B âŸ§á¶œ â†˜ Q â†’
    ---------------------------------------------
    âŸ¦ Arr A B âŸ§á¶œ â†˜ (Î» m â†¦ âˆ€ v, P v â†’ Q (app m v))
end
end

notation:40 "âŸ¦" A:41 "âŸ§áµ›" "â†˜" P:41 => ğ’± A P
notation:40 "âŸ¦" B:41 "âŸ§á¶œ" "â†˜" P:41 => ğ’ B P

theorem interp :
  (âˆ€ A, âˆƒ P, âŸ¦ A âŸ§áµ› â†˜ P) âˆ§
  (âˆ€ B, âˆƒ P, âŸ¦ B âŸ§á¶œ â†˜ P) := by
  refine âŸ¨Î» A â†¦ ?val, Î» B â†¦ ?comâŸ©
  mutual_induction A, B
  case Unit => exact âŸ¨_, .UnitâŸ©
  case Sum ihA ihB =>
    let âŸ¨_, hAâŸ© := ihA
    let âŸ¨_, hBâŸ© := ihB
    exact âŸ¨_, .Sum hA hBâŸ©
  case U ih => let âŸ¨_, hâŸ© := ih; exact âŸ¨_, .U hâŸ©
  case F ih => let âŸ¨_, hâŸ© := ih; exact âŸ¨_, .F hâŸ©
  case Arr ihA ihB =>
    let âŸ¨_, hAâŸ© := ihA
    let âŸ¨_, hBâŸ© := ihB
    exact âŸ¨_, .Arr hA hBâŸ©

def ValType.interp : âˆ€ A, âˆƒ P, âŸ¦ A âŸ§áµ› â†˜ P := _root_.interp.left
def ComType.interp : âˆ€ B, âˆƒ P, âŸ¦ B âŸ§á¶œ â†˜ P := _root_.interp.right

/-*-----------------------------------------------------
  Properties of the logical relation:
  * Interpretation of a type is deterministic
  * Backward closure wrt strong reduction
  * Interpretations contain all strongly neutral terms
  * Terms in interpretations are strongly normalizing
-----------------------------------------------------*-/

theorem determinism :
  (âˆ€ {A P Q}, âŸ¦ A âŸ§áµ› â†˜ P â†’ âŸ¦ A âŸ§áµ› â†˜ Q â†’ P = Q) âˆ§
  (âˆ€ {B P Q}, âŸ¦ B âŸ§á¶œ â†˜ P â†’ âŸ¦ B âŸ§á¶œ â†˜ Q â†’ P = Q) := by
  refine âŸ¨Î» {A P Q} h â†¦ ?val, Î» {B P Q} h â†¦ ?comâŸ©
  mutual_induction h, h
  case Unit => intro h; cases h; rfl
  case Sum ihA ihB =>
    intro h; cases h with | Sum hA hB => rw [ihA hA, ihB hB]
  case U ih =>
    intro h; cases h with | U hB => rw [ih hB]
  case F ih =>
    intro h; cases h with | F hA => rw [ih hA]
  case Arr ihA ihB =>
    intro h; cases h with | Arr hA hB => rw [ihA hA, ihB hB]

def ğ’±.det : âˆ€ {A P Q}, âŸ¦ A âŸ§áµ› â†˜ P â†’ âŸ¦ A âŸ§áµ› â†˜ Q â†’ P = Q := determinism.left
def ğ’.det : âˆ€ {B P Q}, âŸ¦ B âŸ§á¶œ â†˜ P â†’ âŸ¦ B âŸ§á¶œ â†˜ Q â†’ P = Q := determinism.right

theorem ğ’.closure {B P} {m n : Com} (h : âŸ¦ B âŸ§á¶œ â†˜ P) (r : m â¤³â‹† n) : P n â†’ P m := by
  mutual_induction h generalizing m n
  all_goals intro p
  case F =>
    match p with
    | .inl âŸ¨_, r', sneâŸ© => exact Or.inl âŸ¨_, .trans' r r', sneâŸ©
    | .inr âŸ¨_, r', pvâŸ©  => exact Or.inr âŸ¨_, .trans' r r', pvâŸ©
  case Arr hA _ ih => exact Î» v pv â†¦ ih (.app r) (p v pv)

theorem adequacy :
  (âˆ€ {A P} {v : Val}, âŸ¦ A âŸ§áµ› â†˜ P â†’ (SNeVal v â†’ P v) âˆ§ (P v â†’ SNVal v)) âˆ§
  (âˆ€ {B P} {m : Com}, âŸ¦ B âŸ§á¶œ â†˜ P â†’ (SNeCom m â†’ P m) âˆ§ (P m â†’ SNCom m)) := by
  refine âŸ¨Î» h â†¦ ?val, Î» h â†¦ ?comâŸ©
  mutual_induction h, h
  case Unit =>
    refine âŸ¨Î» sne â†¦ Or.inl sne, Î» sn â†¦ ?_âŸ©
    cases sn
    case inl sne => let âŸ¨_, eâŸ© := sne; subst e; exact .var
    case inr e => subst e; constructor
  case Sum ihl ihr v =>
    refine âŸ¨Î» sne â†¦ Or.inl sne, Î» sne â†¦ ?_âŸ©
    match sne with
    | .inl h => let âŸ¨_, eâŸ© := h; subst e; exact .var
    | .inr (.inl âŸ¨_, e, pvâŸ©) => subst e; exact .inl (ihl.right pv)
    | .inr (.inr âŸ¨_, e, qvâŸ©) => subst e; exact .inr (ihr.right qv)
  case U ih v =>
    let âŸ¨sneval, snvalâŸ© := @ih (force v)
    exact âŸ¨Î» sne â†¦ sneval (.force sne),
           Î» sn â†¦ (snval sn).force_invâŸ©
  case F ih m =>
    refine âŸ¨Î» sne â†¦ Or.inl âŸ¨_, .refl, sneâŸ©, Î» sn â†¦ ?_âŸ©
    match sn with
    | .inl âŸ¨_, r, sneâŸ© => exact r.red (.ne sne)
    | .inr âŸ¨_, r, pvâŸ©  => exact r.red (.ret (ih.right pv))
  case Arr ihv ihm _ =>
    refine âŸ¨Î» sne â†¦ ?sne, Î» sn â†¦ ?snâŸ©
    case sne m =>
      exact Î» v pv â†¦ ihm.left (.app sne (ihv.right pv))
    case sn =>
      exact extensionality (ihm.right (sn (var 0) (ihv.left .var)))

def ğ’±.sneVal {A P v} (h : âŸ¦ A âŸ§áµ› â†˜ P) : SNeVal v â†’ P v := (adequacy.left h).left
def ğ’.sneCom {B P m} (h : âŸ¦ B âŸ§á¶œ â†˜ P) : SNeCom m â†’ P m := (adequacy.right h).left
def ğ’±.snVal {A P v} (h : âŸ¦ A âŸ§áµ› â†˜ P) : P v â†’ SNVal v := (adequacy.left h).right
def ğ’.snCom {B P m} (h : âŸ¦ B âŸ§á¶œ â†˜ P) : P m â†’ SNCom m := (adequacy.right h).right
